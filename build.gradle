import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
    id 'io.github.0ffz.github-packages'
}

apply from: 'https://raw.githubusercontent.com/MineInAbyss/shared-gradle/master/common.gradle'

group 'com.derongan.minecraft'
version plugin_version

repositories {
    mavenCentral()
    jcenter()
    // maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://erethon.de/repo/' } //HeadLib
    maven { url 'http://nexus.okkero.com/repository/maven-releases/' }
    maven { url 'https://repo.codemc.io/repository/nms/' }
    maven githubPackage.invoke("MineInAbyss/guiy")
    maven githubPackage.invoke("MineInAbyss/DeeperWorld")
    maven githubPackage.invoke("MineInAbyss/Idofront")
    maven githubPackage.invoke("MineInAbyss/geary")
    maven githubPackage.invoke("MineInAbyss/KotlinSpice")
    maven { url 'https://jitpack.io' } //JitPack
    mavenLocal()
}

publishing {
    repositories {
        maven githubPackage.invoke("MineInAbyss/MineInAbyss")
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$server_version"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "org.cultofclang.minecraft:kotlinspice:$kotlin_version+"
    compileOnly ('com.github.MilkBowl:VaultAPI:1.7') {
        exclude group: 'org.bukkit'
    }
//    compileOnly 'com.badlogicgames.ashley:ashley:1.7.3'

    implementation "com.okkero.skedule:skedule:1.2.6"
    implementation 'de.erethon:headlib:3.0.4'

    implementation 'com.mineinabyss:geary:1.0.3'
    implementation 'com.derongan.minecraft:guiy:0.1.0-alpha.7'
    implementation 'com.mineinabyss:idofront:0.3.25'
    compileOnly 'com.derongan.minecraft:deeperworld:0.3.32'
}

shadowJar {
    getArchiveClassifier().set(null)
    relocate 'com.derongan.minecraft.guiy', "com.derongan.minecraft.guiy.${project.group}.${project.name}"
    relocate 'com.mineinabyss.idofront', "com.mineinabyss.idofront.${project.group}.${project.name}"
    minimize {
        exclude(dependency('de.erethon:headlib:.*'))
    }
}
build.dependsOn shadowJar

compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
